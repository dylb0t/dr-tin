#nop ;
#nop A note on Syntax. You must include semicolon after action statements.;
#nop Also, you must not include semicolons in the middle of your comments.;
#nop ;

#nop ;
#nop Aliases;
#nop ;

#nop ;
#nop Navigation with the numerical phone pad;
#nop There is an overrides file available for PC use;
#nop This is temporary while I write a button logic system;
#nop ;

#alias 1 nw;
#alias 2 n;
#alias 3 ne;
#alias 4 w;
#alias 5 out;
#alias 6 e;
#alias 7 sw;
#alias 8 s;
#alias 9 se;

#nop ;
#nop Magic System;
#nop ;

#nop ;
#nop Spellcasting;
#nop ;

#alias .targ {
	#delay {$CURRENT_RT} {
	 #send prepare %1 %2;
	 #send target %3;

	 #action {^Your formation of a targeting pattern}
	 {
		#delay {$CURRENT_RT} {
		 #if {%4} {
			#send barrage %4;
		 } {
		 	#send cast;
		 }
		 #unaction {^Your formation of a targeting pattern};
		};
	 };
	};

}

#alias .barr {
	#delay {$CURRENT_RT} {
	 #send prepare %1 %2;
         #send target %3;
	  #action {^Your formation of a targeting pattern}
          {
		#delay {$CURRENT_RT} {
                 #send barr slice;
                 #unaction {^Your formation of a targeting pattern};
		};
          };	
	};
}

#alias .cast {
        #delay {$CURRENT_RT} {
	 #send prepare %1 %2;
	 #action {^You feel fully prepared}
	 {
		#send cast;
		#unaction {^You feel fully prepared};
	 };
	};
}

#nop ;
#nop Summoning ;
#nop ;

#alias .align {
	#send align %1;
}

#alias .admit {
        #action {^Finishing your meditation, you feel that you have reached your limit.}
        {
               .stopsum;
        };
        #action {^You so heavily embody the}
        {
               .stopsum;
        };

	#delay {$CURRENT_RT} {
         #action {you feel that you can still gather } 
         {
                .admit;
         };

	 #send summon admittance;
	};
}
#alias .imped {
        #action {exhausted your planar charge}
        {
               .stopsum;
        };
        #action {^Try though you might}
        {
               .stopsum;
        };

	#delay {$CURRENT_RT} {
         #action {you feel that you still have}
         {
                .imped;
         };

         #send summon impedance;
	};
}
#alias .stopsum {
	#unaction {you feel that you still have};
        #unaction {exhausted your planar charge};                                                                       
	#unaction {^Try though you might};
	#unaction {you feel that you can still gather };                                                                 
	#unaction {^Finishing your meditation, you feel that you have reached your limit.};
	#unaction {^You so heavily embody the};
}

#alias .trainsum {

	#nop You must discharge;
	#ticker gaze {gaze cry} 300;
	#ticker aether {align aether; exp;} 186;
	#send align aether;
	#delay {elec} {#ticker {electric} {align electric; exp;} {186}; align electric;} {31};
	#delay {fire} {#ticker {fire} {align fire; exp;} {186}; align fire;} {62};
	#delay {water} {#ticker {water} {align water; exp;} {186}; align water;} {93};
	#delay {earth} {#ticker {earth} {align earth; exp;} {186}; align earth;} {124};
	#delay {air} {#ticker {air} {align air; exp;} {186}; align air;} {155};

}

#alias .trainstop {
	#undelay elec; #undelay fire; #undelay water; #undelay earth; #undelay air; #undelay gaze;
	#untick aether; #untick electric; #untick fire; #untick water; #untick earth; #untick air; #untick gaze;
	#showme Stopped;
}

#nop ;
#nop Hunting;
#nop ;

#nop ;
#nop Skinning;
#nop ;

#alias .skin {
	#var weapon {sword};
        #action {to see a %%1 in your right hand}
        {
               #var weapon %%1;
        };
	#delay {$CURRENT_RT} {	
	 #send hands;

	 #delay {$CURRENT_RT+.25} {
	  #send get my skinning knife;
	  #send sheath $weapon in my sheath;
	  
	   #delay {$CURRENT_RT+.25} {
	    #send skin %1;
	    #delay {$CURRENT_RT+.5} {
	     #send put my skin knife in my belt;
	     #send wield my $weapon;
	     #delay {$CURRENT_RT+.5} {
	      #unaction {to see a %%1 in your right hand};
	      #send search %1;
	     };
            };
           };
          };
	 
	};

}

#nop ;
#nop Prepare for battle;
#nop ;

#alias .prepbat {
	.cast z 20;
	#action {^You gesture.}
	{
		#unaction {^You gesture.};
		.cast ys 20;
		#action {^You gesture.}
        	{
			#unaction {^You gesture.};
			.cast suf 20;
			#action {^You gesture.}
                	{
				#unaction {^You gesture.};
				.cast sw 20;
				#action {^You gesture.}
                        	{
					#unaction {^You gesture.};
					#send get clay from sheath;
					prep ignite 20;
					#action {^You feel fully prepared}
					{
						#unaction {^You feel fully prepared};
						cast clay;
					};
				};
			};
		};
	};
}

#nop ;
#nop Following Directions;
#nop ;

#alias .dir {
	#send dir %1 %2 %3 %4;
	#action {^Directions towards %+: %%1.}
	{
		#send %%1;
	};
	#action {^You made it}
	{
		.dirstop;
	};
}

#alias .dirstop {
	#send dir stop;
	#unaction {^Directions towards %+: %%1.};
	#unaction {^You made it};
}







