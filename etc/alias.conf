#nop Aliases
#nop Navigation with the numerical pad

#alias 1 nw;
#alias 2 n;
#alias 3 ne;
#alias 4 w;
#alias 5 out;
#alias 6 e;
#alias 7 sw;
#alias 8 s;
#alias 9 se;


#nop Spellcasting
#alias .targ {
	#send prepare %1 %2;
	#send target %3;

	#action {^Your formation of a targeting pattern}
	{
		#send cast;
		#unaction {^Your formation of a targeting pattern};
	};

}

#alias .barr {
	#delay {$CURRENT_RT} {
	 #send prepare %1 %2;
         #send target %3;
	  #action {^Your formation of a targeting pattern}
          {
		#delay {$CURRENT_RT} {
                 #send barr slice;
                 #unaction {^Your formation of a targeting pattern};
		};
          };	
	};
}

#alias .spell {
	#send prepare %1 %2;
	#action {^You feel fully prepared}
	{
		#send cast;
		#unaction {^You feel fully prepared};
	};
}

#nop Summoning Training

#alias .align {
	#send align %1;
}

#alias .admit {
	#delay {$CURRENT_RT} {
	 #send summon admit;
	 #action {you feel that you can still gather a bigger charge} {
		.admit;
	 	#unaction {you feel that you can still gather a bigger charge}
	 }
	};
        #action {^Finishing your meditation, you feel that you have reached your limit.}
        {
                #unaction {^Your formation of a targeting pattern};
                #unaction {^Finishing your meditation, you feel that you have reached your limit.}
		#unaction {^You so heavily embody the}
        };
	#action {^You so heavily embody the}
	{
               #unaction {^Your formation of a targeting pattern};
                #unaction {^Finishing your meditation, you feel that you have reached your limit.}
                #unaction {^You so heavily embody the}
	};
}

#alias .trainsum {

	#ticker gaze {gaze cry} 300;
	#ticker aether {align aether; exp;} 186;
	#send align aether;
	#delay {elec} {#ticker {electric} {align electric; exp;} {186}; align electric;} {31};
	#delay {fire} {#ticker {fire} {align fire; exp;} {186}; align fire;} {62};
	#delay {water} {#ticker {water} {align water; exp;} {186}; align water;} {93};
	#delay {earth} {#ticker {earth} {align earth; exp;} {186}; align earth;} {124};
	#delay {air} {#ticker {air} {align air; exp;} {186}; align air;} {155};

}

#alias .trainstop {
	#undelay elec; #undelay fire; #undelay water; #undelay earth; #undelay air; #undelay gaze;
	#untick aether; #untick electric; #untick fire; #untick water; #untick earth; #untick air; #untick gaze;
	#showme Stopped;
}


#nop Skinning
#alias .skin {
	#nop #var weapon {claymore};
	#delay {$CURRENT_RT} {	
	 #nop #send hands;
	 #action {to see a %%1 in your right hand}
	 {
		#var $weapon %%1;
		#send sheath $weapon in my sheath;
		#unaction {to see a %%1 in your right hand}
	 };
	 #send hands;
	 #delay {$CURRENT_RT + 1} {
	 #send get my skin knife;};
	 #delay {$CURRENT_RT + 2} {
	 #send skin;};
	 #delay {$CURRENT_RT + 3} {
	  #send put my skin knife in my belt;
	  #send wield my $weapon;
	 };
	 delay 10 {#unaction {to see a %%1 in your right hand}};
	};
}


#nop Prepare for battle
#alias .prepbat {
	.spell es 20;
	#action {^You gesture.}
	{
		#unaction {^You gesture.};
		.spell ys 20;
		#action {^You gesture.}
        	{
			#unaction {^You gesture.};
			.spell suf 20;
			#action {^You gesture.}
                	{
				#unaction {^You gesture.};
				.spell sw 20;
				#action {^You gesture.}
                        	{
					#unaction {^You gesture.};
					#send get sword from sheath;
					prep ignite 20;
					#action {^You feel fully prepared}
					{
						#unaction {^You feel fully prepared};
						cast sword;
					};
				};
			};
		};
	};
}

#nop Following Directions
#alias .dir {
	#send dir %1 %2 %3 %4;
	#action {^Directions towards %+: %%1.}
	{
		#send %%1;
	}
	#action {^You made it}
	{
		.dirstop;
		#unaction {^You made it}
	}
}

#alias .dirstop {
	#unaction {^Directions towards %+: %%1.}
}







